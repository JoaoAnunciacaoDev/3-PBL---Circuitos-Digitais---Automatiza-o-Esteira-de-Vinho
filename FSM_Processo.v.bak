module FSM_Processo (
    input clk, Reset,
    input Start_Pressionado, Motor_Pausado_Em_Posicao, Sensor_Garrafa_Cheia,
    input Rolha_Disponivel, Botao_Vedar, Botao_Enter_CQ, Input_Qualidade_OK,
    input Botao_Lacre_e_Conta,
    output reg Comando_Mover_Esteira, Valv_Enchimento, Valv_Vedacao,
    output reg Menos_Rolha, LED_Descarte, Incrementa_Duzia
);
    reg [2:0] estado_atual, proximo_estado;
    parameter PARADO = 3'b000, ENCHENDO = 3'b001, AGUARDANDO_VEDACAO = 3'b010,
              AGUARDANDO_CQ = 3'b100, AGUARDANDO_LACRE = 3'b101;

    always @(posedge clk or posedge Reset) begin
        if (Reset) estado_atual <= PARADO;
        else estado_atual <= proximo_estado;
    end

    always @(*) begin
        proximo_estado = estado_atual;
        case (estado_atual)
            PARADO: if (Start_Pressionado) proximo_estado = ENCHENDO;
            ENCHENDO: if (Motor_Pausado_Em_Posicao && Sensor_Garrafa_Cheia) proximo_estado = AGUARDANDO_VEDACAO;
            AGUARDANDO_VEDACAO: if (Botao_Vedar && Rolha_Disponivel) proximo_estado = AGUARDANDO_CQ;
            AGUARDANDO_CQ: begin
                if (Motor_Pausado_Em_Posicao && Botao_Enter_CQ) begin
                    if (Input_Qualidade_OK) proximo_estado = AGUARDANDO_LACRE;
                    else proximo_estado = ENCHENDO;
                end
            end
            AGUARDANDO_LACRE: if (Motor_Pausado_Em_Posicao && Botao_Lacre_e_Conta) proximo_estado = ENCHENDO;
        endcase
    end

    always @(*) begin
        Comando_Mover_Esteira=0; Valv_Enchimento=0; Valv_Vedacao=0;
        Menos_Rolha=0; LED_Descarte=0; Incrementa_Duzia=0;
        case (estado_atual)
            PARADO: if (Start_Pressionado) Comando_Mover_Esteira = 1'b1;
            ENCHENDO: if (Motor_Pausado_Em_Posicao) Valv_Enchimento = 1'b1;
            AGUARDANDO_VEDACAO:
                if (Botao_Vedar && Rolha_Disponivel) begin
                    Valv_Vedacao = 1'b1; Menos_Rolha = 1'b1; Comando_Mover_Esteira = 1'b1;
                end
            AGUARDANDO_CQ:
                if (Motor_Pausado_Em_Posicao && Botao_Enter_CQ) begin
                    if (!Input_Qualidade_OK) LED_Descarte = 1'b1;
                    Comando_Mover_Esteira = 1'b1;
                end
            AGUARDANDO_LACRE:
                if (Motor_Pausado_Em_Posicao && Botao_Lacre_e_Conta) begin
                    Incrementa_Duzia = 1'b1; Comando_Mover_Esteira = 1'b1;
                end
        endcase
    end
endmodule